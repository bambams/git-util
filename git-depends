#!/usr/bin/env perl

use v5.012;
use strict;
use warnings;

use Cwd qw/abs_path/;

chomp(my $root = `git-root`);

chdir "$root" || exit $?;

my $path = ".gitdepends";

my ($pattern) = @ARGV;

shift @ARGV;

my $filter = defined $pattern ?
        sub { @$_ && $_->[0] =~ /\Q$pattern\E/ } :
        sub { @$_ };

if (-f $path) {
    open my $fh, '<', $path or die "open: $!";

    chomp(my @dependencies = <$fh>);

    close $fh or warn "close: $!";

    @dependencies = grep $filter->(),
            map [/^([\w]+)\s+(.*)/], @dependencies;

    if (defined $pattern && !@dependencies) {
        die "No dependencies match /\\Q$pattern\\E/.\n";
    }

    for my $dependency (@dependencies) {
        my ($name, $repo) = @$dependency;

        unless($repo =~ /:/) {
            $repo = abs_path($repo);
        }

        print "$name: $repo\n";

        if(@ARGV) {
            $ENV{GIT_DIR} = "$repo/.git";
            $ENV{GIT_WORK_TREE} = $repo;

            system('git', @ARGV);

            my ($status, $signal) = ($? >> 8, $? & 127);

            if($signal) {
                kill $signal, $$;
            }

            if($status) {
                exit($status);
            }
        }
    }
}
